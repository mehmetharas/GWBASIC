10                  '**************KASA HAREKETLERI PROGRAMI**************
15 CLS
20                     '-------------------------------------------
30 OPEN "R",#2,"KASA.DAT",26
40 FIELD #2,10 AS T$,8 AS DEVIR$,8 AS GUNLUK$
50               'DIKKAT!..BU DOSYA TEK KAYITTAN OLUSMAKTADIR
60 GOSUB 660
70 CLS:KEY OFF:GOSUB 460
80 LOCATE 2,10:PRINT"PROGRAM MENUSU"
90 LOCATE 5,5:PRINT"1...KASA HAREKETLERI"
100 LOCATE 7,5:PRINT"2...KASA GOZLEMI"
110 LOCATE 9,5:PRINT"3...PROGRAM CIKISI"
120 LOCATE 21,3:PRINT"SECIMINIZ(1,2,3)"
130 LOCATE 21,40:INPUT" ",C
140 GET #2,1             'DOSYADAKI KAYITLAR OKUNUR
150 ON C GOTO 170,180,190
160 GOTO 70
170 GOSUB 210:GOTO 70
180 GOSUB 360:GOTO 70
190 CLOSE #2:END
200              '***KASAYA PARA GIRISI VEYA CIKISI*********
210 CLS:GOSUB 460                'EKRANDA CERCEVE CIZER
220 LOCATE 12,10:INPUT"MIKTARI GIRINIZ:",ODE#
230 LOCATE 14,10:INPUT"KASAYA GIRIYORSA (G) AKSI HALDE (C) GIRINIZ:",KOD$
240 IF KOD$="G" OR KOD$="C" THEN 250 ELSE 230
250 IF KOD$="C" THEN ODE#=-ODE#
260 LOCATE 16,10:INPUT"GUNUN TARIHINI DUZGUN GIRINIZ GG-AA-YYYY:",TAR$
270 DEVIR#=CVD(DEVIR$):GUNLUK#=CVD(GUNLUK$)
280 IF T$=TAR$ THEN GUNLUK#=GUNLUK#+ODE#:GOTO 300
290 DEVIR#=DEVIR#+GUNLUK#:GUNLUK#=ODE#
300 LSET T$=TAR$
310 LSET GUNLUK$=MKD$(GUNLUK#)
320 LSET DEVIR$=MKD$(DEVIR#)
330 PUT# 2,1
340 RETURN
350             '***KASA GOZLEMI***************************
360 CLS:GOSUB 460
370 DEVIR#=CVD(DEVIR$):GUNLUK#=CVD(GUNLUK$)
380 LOCATE 10,10:PRINT"DEVREDEN KASA YEKUNU:"
390 LOCATE 10,40:PRINT USING"##########.##";DEVIR#
400 LOCATE 12,10:PRINT"GUNLUK KASA YEKUNU:"
410 LOCATE 12,40:PRINT USING"##########.##";GUNLUK#
420 LOCATE 14,10:PRINT"TOPLAM KASA YEKUNU:"
430 LOCATE 14,40:PRINT USING"##########.##";GUNLUK#+DEVIR#
440 LOCATE 22,20:INPUT"GOZLEM BITTIYSE <RETURN> A BASINIZ",C$
450 RETURN
460               '***SUSLEME********************************
470 LOCATE 1,1:PRINT SOLUST$
480 FOR I=2 TO 79:LOCATE 1,I:PRINT YATAY$:NEXT
490 LOCATE 1,80:PRINT SAGUST$
500 LOCATE 2,1:PRINT DUSEY$:LOCATE 2,80:PRINT DUSEY$
510 LOCATE 3,1:PRINT SOLORT$
520 FOR I=2 TO 79:LOCATE 3,I:PRINT YATAY$:NEXT
530 LOCATE 3,80:PRINT SAGORT$
540 FOR I=4 TO 20
550 LOCATE I,1:PRINT DUSEY$:LOCATE I,80:PRINT DUSEY$
560 NEXT
570 LOCATE 21,1:PRINT SOLORT$
580 FOR I=2 TO 79:LOCATE 21,I:PRINT YATAY$:NEXT
590 LOCATE 21,80:PRINT SAGORT$
600 LOCATE 22,1:PRINT DUSEY$:LOCATE 22,80:PRINT DUSEY$
610 LOCATE 23,1:PRINT SOLALT$
620 FOR I=2 TO 79:LOCATE 23,I:PRINT YATAY$:NEXT
630 LOCATE 23,80:PRINT SAGALT$
640 RETURN
650             '***SUSLEME ICIN TANIMLAR YAPAR***********
660 SOLUST$=CHR$(201)
670 SAGUST$=CHR$(187)
680 SOLALT$=CHR$(200)
690 SAGALT$=CHR$(188)
700 SOLORT$=CHR$(204)
710 SAGORT$=CHR$(185)
720 YATAY$=CHR$(205)
730 DUSEY$=CHR$(186)
740 RETURN
