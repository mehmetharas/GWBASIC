10            '**********MUSTERI UYGULAMASI**********
20                             '_____________________
30 CLS:KEY OFF
40 S=7
50 DIM A$(S)
60 DIM A1$(S)
70 OPEN"R",#1,"MUSTERI.DAT",87
80 FIELD#1,1 AS A$(0),14 AS A$(1),20 AS A$(2),20 AS A$(3),8 AS A$(4),14 AS A$(5),2 AS A$(6),8 AS A$(7)
90           '**********PROGRAM MENUSU***************
100 CLS
110 LOCATE 2,30:PRINT "MUSTERI MENUSU"
120 LOCATE 7,10:PRINT "1-GIRIS"
130 LOCATE 9,10:PRINT "2-DUZELTME"
140 LOCATE 11,10:PRINT "3-SILME"
150 LOCATE 13,10:PRINT "4-GOZLEM"
160 LOCATE 15,10:PRINT "5-LISTELEME"
170 LOCATE 17,10:PRINT "6-ODEME"
180 LOCATE 19,10:PRINT "7-PROGRAM SONU"
190 LOCATE 22,10:PRINT "SECIMINIZI GIRINIZ<1,2,3,4,5,6,7>"
200 LOCATE 22,45:INPUT A
210 ON A GOTO 240,310,400,520,590,610,680
220 GOTO 100
230           '***GIRIS*******************************
240 GOSUB 730
250 IF A$(0)="*"THEN 330
260 GOSUB 830
270 GOSUB 940
280 GOSUB 1120
290 IF EVET$="E"THEN 240 ELSE 100
300       '*******DUZELTME****************************
310 GOSUB 730
320 IF A$(0)<>"*"THEN 260
330 GOSUB 830
340 GOSUB 790
350 GOSUB 1160
360 GOSUB 940
370 GOSUB 1120
380 IF EVET$="E"THEN 310 ELSE 100
390       '*********SILME*****************************
400 GOSUB 730
410 IF A$(0)<>"*"THEN 100
420 GOSUB 790:GOSUB 830:GOSUB 1160
430 LOCATE 2,65:COLOR 0,7:PRINT"SILME":COLOR 7,0
440 LOCATE 22,40:COLOR 31,0:INPUT"Silinecek mi? <E/H>",SIL$
450 COLOR 7,0:LOCATE 22,40: PRINT "    "
460 IF SIL$<>"E"THEN 100
470 LSET A$(0)="@"
480 PUT #1,KOD%
490 GOSUB 1120
500 IF EVET$="E" THEN 400 ELSE 100
510        '****GOZLEM*******************************
520 GOSUB 730
530 IF A$(0)<>"*"THEN 250
540 GOSUB 790:GOSUB 830:GOSUB 1160
550 LOCATE 2,65:COLOR 0,7:PRINT "GOZLEM":COLOR 7,0
560 GOSUB 1120
570 IF EVET$="E"THEN 520 ELSE 100
580      '************LISTELEME**********************
590 GOSUB 1230:GOTO 100
600      '***************ODEME***********************
610 GOSUB 730
620 IF A$(0)="@" THEN LOCATE 22,2: INPUT"KAYIT SILINMIS..<RETURN>..",L$:GOTO 100
630 IF A$(0)<>"*"THEN LOCATE 22,2: INPUT"KAYIT HENUZ GIRILMEMIS..<RETURN>",L$:GOTO 100
640 GOSUB 1560
650 GOSUB 1120
660 IF EVET$="E" THEN 610
670       '**********PROGRAM SONU********************
680 CLOSE#1
690 END
700       '********ALT PROGRAMLAR********************
710       '********++++++++++++++********************
720       '***EKRANDAN KAYIT NO ALIRSA, DOSYAYI OKUR*
730 CLS:LOCATE 10,10:INPUT"Musteri Nosunu Giriniz:",KOD%
740 IF KOD%=0 THEN 730
750 GOSUB 1210
760 GET #1,KOD%
770 RETURN
780      '****BUFFER I DEGISKENLERE ATAR*************
790 FOR I%=1 TO S-1 :A1$(I%)=A$(I%): NEXT
800 A1#=CVD(A$(S))
810 RETURN
820      '****EKRANA SABITLERI GOSTERIR**************
830 CLS:LOCATE 2,10
840 COLOR 0,15:PRINT"GIRIS/DUZELTME/GOZLEM BILGILERI":COLOR 7,0
850 LOCATE 4,10 :PRINT"Musteri Adi    :"
860 LOCATE 6,10:PRINT"Musteri Adres-1   :"
870 LOCATE 8,10:PRINT"Musteri Adres-2   :"
880 LOCATE 10,10:PRINT"Vergi numarasi   :"
890 LOCATE 12,10:PRINT"Vergi Dairesi    :"
900 LOCATE 14,10:PRINT "Il Kodu      :"
910 LOCATE 16,10:PRINT"Musteri Borcu  :"
920 RETURN
930    '***EKRANDAN DEGISKENLERI ALIR BYFFERA KOYAR KAYDI YAZAR***
940 LOCATE 2,65:COLOR 0,7
950 K=4
960 IF A$(0)<>"*" THEN PRINT"YENI GIRIS" ELSE PRINT"DUZELTME"
970 COLOR 7,0
980 FOR I%=1 TO S-1
990 LOCATE K,30:INPUT " ",A1$(I%)
1000 IF A1$(I%)<>" " THEN LSET A$(I%)=A1$(I%)
1010 K=K+2
1020 NEXT
1030 LOCATE K,30:INPUT " ",A1#
1040 IF A1#<>0 THEN LSET A$(7)=MKD$(A1#)
1050 LOCATE 22,3:COLOR 0,7:PRINT"GIRISLER DOGRUMU<E/H>":COLOR 7,0
1060 LOCATE 22,34:INPUT K$
1070 IF K$="H"THEN K=4:GOTO 980
1080 LSET A$(0)="*"
1090 PUT #1,KOD%
1100 RETURN
1110   '******EKRANDAN Evet/Hayir CEVABI ALIR************
1120 LOCATE 22,45:PRINT"DEVAMMI<E/H>":COLOR 7,0
1130 LOCATE 22,70:INPUT" ",EVET$
1140 RETURN
1150    '****DEGISKENLERI EKRANA GORUNTULER**************
1160 K=4:COLOR 0,7
1170 FOR I%=1 TO S-1:LOCATE K,31:PRINT A1$(I%) :K=K+2  :NEXT
1180 LOCATE K,31:PRINT USING"##########,.##";A1#
1190 COLOR 7,0:RETURN
1200     '************BUFFER A BOSLUK TASIR**************
1210 FOR I%=1 TO S-1: LSET A$(I%)=" " : NEXT
1220 LSET A$(S)=MKD$(0)  :RETURN
1230   '************LISTELEME BOLUMU*********************
1240  GOSUB 1390
1250 GOSUB 1450 : T=4
1260 FOR KOD%=L1% TO L2%
1270 GET #1,KOD% : IF A$(0)<>"*" THEN 1360
1280 IF IL$<>""AND A$(6)<>IL$ THEN 1360
1290 GOSUB 790
1300 LOCATE T,2:PRINT KOD%
1310 LOCATE T,9:PRINT A$(1)
1320 LOCATE T,37:PRINT A$(6)
1330 LOCATE T,45:PRINT USING"##########,.##";A1#
1340 T=T+1
1350 IF T=21 THEN GOSUB 1120:1450:T=4
1360 NEXT
1370 GOSUB 1120:IF EVET$="E" OR EVET$="H"THEN 100: RETURN: ELSE 1240
1380   '***LISTELENECEK ILK VE SON KAYIT NOTLARINI ALIR***
1390 CLS
1400 LOCATE 4,10:INPUT "LISTELENECEK IL KODUNU GIRINIZ:",IL$
1410 LOCATE 10,10:INPUT "Ilk Musteri No...:",L1%:IF L1%=0 OR(L1%>INT(LOF(1)/87))THEN L1%=1
1420 LOCATE 12,10:INPUT"Son Musteri NO..:",L2%:IF L2%=0 OR (L2%>INT(LOF(1)/87)) THEN L2%=INT(LOF(1)/87)
1430 RETURN
1440      '*****EKRANA BASLIK ATAR**************************
1450 CLS
1460 LOCATE 2,2:PRINT"KOD"
1470 LOCATE 2,9:PRINT"MUSTERI ADI      "
1480 LOCATE 2,33:PRINT"IL KODU    "
1490 LOCATE 2,46:PRINT"BORCU   "
1500 LOCATE 3,2:PRINT"==="
1510 LOCATE 3,9:PRINT"===================="
1520 LOCATE 3,33:PRINT"=========="
1530 LOCATE 3,46:PRINT"============="
1540 RETURN
1550     '******ODEME ISLEMI********************************
1560 GOSUB 790:GOSUB 830:GOSUB 1160
1570 LOCATE 18,10:INPUT"Odenen Miktari Giriniz:",ODE#
1580 LOCATE 18,53:COLOR 31,7:PRINT"MUSTERI ve KASA ya isleniyor"'
1590 A1#=A1#-ODE#
1600 LSET A$(7)=MKD$(A1#):LOCATE 20,10: COLOR 0,7
1610 PRINT USING"Musterinin kalan borcu##########,.##";A1#
1620 PUT#1,KOD%
1630 LOCATE 23,10:COLOR 7,0:INPUT"Kasa icin gunun tarihini duzgun giriniz GG-AA-YYYY";TAR$
1640 OPEN "R",#2,"KASA.DAT",26
1650 FIELD #2,10 AS T$,8 AS DEVIR$,8 AS GUNLUK$
1660 GET#2,1
1670 DEVIR#=CVD(DEVIR$):GUNLUK#=CVD(GUNLUK$)
1680 IF T$=TAR$ THEN GUNLUK#=GUNLUK#+ODE#:GOTO 1720
1690               'Kasa tarihi(T$)odeme tarihine(TAR$)esit ise GUNLUK KASA HAREKETi sozkonusudur.Aksi halde DEVIR islemi gerekir.
1700 DEVIR#=DEVIR#+GUNLUK#:GUNLUK#=ODE#
1710 LSET T$=TAR$:LSET DEVIR$=MKD$(DEVIR#)
1720 LSET GUNLUK$=MKD$(GUNLUK#)
1730 PUT#2,1
1740 CLOSE#2: DEVIR#=0 : GUNLUK#=0 : RETURN
