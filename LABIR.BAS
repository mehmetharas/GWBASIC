1 CLS:REM ---  TšRK˜YE - MEHMET HARAS, N˜SAN 1991
2 MUN$="t255mbl64o1afgao4d":PI$="mbl64abceabceebceabceagaa":'fix was in 780
3 DEFINT A-Z
4 TH$="mbt190o2l8bbbl16cecl8bbp8bbbl16cl8edcc"
10 DEFSTR A,V,U,S,D,R,B:DEFINT W,I,J,X,Y,N,P,T,M:DIM P(5,10)
12 CLS:PLAY TH$:FOR I=60 TO 1 STEP -1:LOCATE 14,I:PRINT CHR$(2);" ";:FOR J=1 TO 25:NEXT J:NEXT I:PLAY TH$:FOR I=1 TO 60:LOCATE 14,I:PRINT " ";CHR$(3);"   ";CHR$(4);CHR$(5);CHR$(6);"       ";CHR$(2);:FOR J=1 TO 25:NEXT J:NEXT I
15 W=0:I=0:J=0:X=0:Y=0:N=0:P=0:T=0:M=0
20 DEF FNLAP%(N)=360*VAL(LEFT$(TIME$,2))+60*VAL(MID$(TIME$,4,2))+VAL(RIGHT$(TIME$,2))
30 CLS:B=" ":V=CHR$(219):U=B+CHR$(186):A=CHR$(196):B1="ba#ag#gf#fed#dc#c":B2="cc#dd#eff#gg#aa#b"
40 D=B+CHR$(249): R=B+CHR$(219): S=STRING$(2,205):SS=CHR$(205)
50 COLOR 15,0:KEY OFF:LOCATE 25,28:PRINT " L A B ˜ R E N T  ";:COLOR 7,0:PRINT "        MEHMET HARAS 29-04-1991";
60 LOCATE 24,1:PRINT USING "&";STRING$(79,223);
70 LOCATE 1,1:PRINT STRING$(79,220);
80 LOCATE 2,1:PRINT V;D;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;D;D;R;
90 LOCATE 3,1:PRINT V;D;U;D;B;CHR$(201);S;S;S;D;U;D;B;S;S;S;S;CHR$(203);S;S;D;U;D;B;S;S;CHR$(203);S;S;S;S;D;U;D;B;S;S;S;S;CHR$(187);D;U;D;R;
100 LOCATE 4,1:PRINT V;D;U;D;U;D;D;D;D;D;D;D;D;D;D;U;D;D;D;U;D;D;D;U;D;D;D;D;D;D;D;D;D;D;D;U;D;U;D;R;
110 LOCATE 5,1:PRINT V;D;U;D;U;D;B;S;S;S;S;S;S;S;SS;D;U;D;B;S;S;CHR$(202);S;S;D;U;D;B;S;S;S;S;S;S;S;S;SS;D;U;D;U;D;R;
120 LOCATE 6,1:PRINT V;D;U;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;U;D;R;
130 LOCATE 7,1:PRINT V;D;U;D;B;S;S;S;S;SS;D;B;S;S;S;CHR$(187);D;B;CHR$(201);S;S;S;SS;CHR$(203);S;S;S;SS;CHR$(203);S;S;D;B;CHR$(201);S;S;D;B;SS;S;S;S;S;D;U;D;R;
140 LOCATE 8,1:PRINT V;D;U;D;D;D;D;D;D;D;D;D;B;CHR$(201);SS;CHR$(188);D;U;D;D;D;U;D;D;D;U;D;D;D;U;D;D;D;D;D;D;D;D;D;U;D;R;
150 LOCATE 9,1:PRINT V;D;U;D;U;D;B;SS;S;S;S;SS;CHR$(187);D;U;D;D;D;D;U;D;D;D;U;D;D;D;U;D;U;D;B;CHR$(201);S;S;S;S;D;U;D;U;D;R;
160 LOCATE 10,1:PRINT V;D;D;D;U;D;D;D;D;D;U;D;U;D;U;D;B;S;CHR$(202);S;S;S;SS;CHR$(202);S;S;D;U;D;U;D;U;D;D;D;D;D;U;D;D;D;R;
170 LOCATE 11,1:PRINT STRING$(9,223);D;U;D;U;D;D;D;U;D;U;D;D;D;D;D;D;D;D;D;D;U;D;U;D;D;D;U;D;U;D;B;STRING$(9,223);
180 LOCATE 12,1:PRINT A;A;A;D;D;D;D;U;D;U;D;U;D;U;D;U;D;B;CHR$(201);S;S;B;B;B;B;B;S;S;CHR$(187);D;U;D;U;D;U;D;U;D;U;D;D;D;D;B;A;A;A;
190 LOCATE 13,1:PRINT STRING$(9,220);D;U;D;U;D;U;D;U;D;D;D;U;STRING$(12,B);U;D;D;D;U;D;U;D;U;D;U;D;B;STRING$(9,220);
200 LOCATE 14,1:PRINT V;D;D;D;U;D;D;D;U;D;U;D;U;D;U;D;U;STRING$(12,B);U;D;U;D;U;D;U;D;U;D;D;D;U;D;D;D;R;
210 LOCATE 15,1:PRINT V;D;U;D;U;D;U;D;D;D;U;D;U;D;U;D;U;STRING$(12,B);U;D;U;D;U;D;U;D;D;D;U;D;U;D;U;D;R;
220 LOCATE 16,1:PRINT V;D;U;D;U;D;B;CHR$(200);S;S;D;U;D;U;D;U;D;B;CHR$(200);STRING$(13,SS);CHR$(188);D;U;D;U;D;U;D;B;S;S;CHR$(188);D;U;D;U;D;R;
230 LOCATE 17,1:PRINT V;D;U;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;U;D;R;
240 LOCATE 18,1:PRINT V;D;U;D;B;S;S;S;S;S;S;CHR$(188);D;B;CHR$(200);STRING$(29,SS);CHR$(188);D;B;CHR$(200);S;S;S;S;S;S;D;U;D;R;
250 LOCATE 19,1:PRINT V;D;U;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;B;B;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;D;U;D;R;
260 LOCATE 20,1:PRINT V;D;U;D;U;D;B;S;S;S;S;S;S;S;SS;D;U;D;B;S;S;CHR$(203);S;S;D;U;D;B;S;S;S;S;S;S;S;S;SS;D;U;D;U;D;R;
270 LOCATE 21,1:PRINT V;D;U;D;U;D;D;D;D;D;D;D;D;D;D;U;D;D;D;U;D;D;D;U;D;D;D;D;D;D;D;D;D;D;D;U;D;U;D;R;
280 LOCATE 22,1:PRINT V;D;U;D;B;CHR$(200);S;S;S;D;U;D;B;S;S;S;S;CHR$(202);S;S;D;U;D;B;S;S;CHR$(202);S;S;S;S;D;U;D;B;S;S;S;S;CHR$(188);D;U;D;R;
290 LOCATE 23,1:PRINT V;D;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;D;D;U;D;D;D;D;D;D;D;D;D;R;
300 COLOR 26,0:LOCATE 6,2:PRINT D;:LOCATE 6,76:PRINT D;:LOCATE 19,2:PRINT D;:
310 LOCATE 2,30:PRINT D;:LOCATE 2,50:PRINT D;:LOCATE 23,30:PRINT D;:LOCATE 23,50:PRINT D;:
320 LOCATE 19,76:PRINT D;:LOCATE 12,18:PRINT D;:LOCATE 12,60:PRINT D;:COLOR 7,0
330 REM
340 REM   initial
350 REM
360 NC=3:NV=468:F=.4:IN=FNLAP%(2):RANDOMIZE(IN):LOCATE 25,1:PRINT "PUAN";NV;:LOCATE 25,15:PRINT STRING$(3,1);
370 P(1,1)=1:P(1,2)=7:P(1,3)=19:P(1,4)=19:P(1,5)=32:
380 P(1,6)=0:P(1,7)=0:P(1,8)=0:P(1,9)=0:P(1,10)=0
390 FOR N=2 TO 5
400 P(N,1)=N+1:P(N,2)=7:P(N,3)=14:P(N,4)=16+N:P(N,5)=32
410 T=RND(2): IF T=0 THEN T=-1
420 P(N,6)=0:P(N,7)=RND(2):P(N,8)=T*(1-P(N,7)):P(N,7)=P(N,7)*T
430 P(N,9)=0:P(N,10)=0 :NEXT
435 REM --- without joysticks take rem from 440-500 and use editing arrows
436 REM ----  see also 640-646
440 KEY(11) ON: KEY(12) ON: KEY(13) ON: KEY(14) ON
450 ON KEY(11) GOSUB 470: ON KEY(12) GOSUB 480: ON KEY(13) GOSUB 490: ON KEY(14) GOSUB 500
460 GOTO 540
470 P(1,9)=-1:P(1,10)=0:RETURN
480 P(1,9)=0:P(1,10)=-1:RETURN
490 P(1,9)=0:P(1,10)=1:RETURN
500 P(1,9)=1:P(1,10)=0:RETURN
510 REM
520 REM    main loop
530 REM
540 GOSUB 648:TL=FNLAP%(2)-IN:IF TL<=1 THEN TL=1
550 FOR M=1 TO TL STEP 180:GOSUB 1140:NEXT:GOTO 540
610 REM
620 REM    move  pac-girl
630 REM
635 REM -----  without joysticks put rem on 640-646
640 REM X=STICK(0):I=9:J=10:IF X<30 THEN IF P(1,8)<0 THEN GOTO 644:ELSE P(1,9)=0:P(1,10)=-1:I=7:J=8:GOTO 644
642 REM IF X>90 THEN IF P(1,8)<=0 THEN P(1,9)=0:P(1,10)=1:I=7:J=8
644 REM Y=STICK(1):IF Y<30 THEN P(1,I)=1:P(1,J)=0:GOTO 648
646 REM IF Y>90 THEN P(1,I)=-1:P(1,J)=0
648 X=P(1,3)+P(1,9): Y=P(1,4)+P(1,10): W=SCREEN(X,1+Y+Y):IF W=32 THEN 770
649 IF W=249 THEN 770
660 IF W<=2 THEN 670
661 IF W<7 THEN 880
670 IF X<>12 THEN 700
671 IF W<>196 THEN 700
680 Y=39-P(1,4): W=SCREEN(X,1+Y+Y):IF W=32 THEN 770
681 IF W=249 THEN 770
700 X=P(1,3)+P(1,7): Y=P(1,4)+P(1,8):W=SCREEN(X,1+Y+Y):IF W=32 THEN 780
701 IF W=249 THEN 780
720 IF W<=2 THEN 730
721 IF W<7 THEN 880
730 IF X<>12 THEN 760
731 IF W<>196 THEN 760
740 Y=39-P(1,4): W=SCREEN(X,1+Y+Y):IF W=32 THEN 780
741 IF W=249 THEN 780
760 P(1,7)=0:P(1,8)=0:SOUND 4000,.2:SOUND 5000,.1:RETURN
770 P(1,7)=P(1,9): P(1,8)=P(1,10)
780 IF SCREEN(X,1+Y+Y,1)>7 THEN PLAY PI$: I=(20+NV/5)/(NC*NC):FOR N=2 TO 5:P(N,2)=26:P(N,6)=I:NEXT
790 LOCATE P(1,3),1+P(1,4)*2:PRINT CHR$(P(1,5));:LOCATE X,1+Y+Y:COLOR P(1,2),0:PRINT CHR$(P(1,1));:COLOR 7,0:P(1,5)=32:P(1,3)=X:P(1,4)=Y:IF W=249 THEN IF NV>0 THEN NV=NV-1:LOCATE 25,5:PRINT NV;:PLAY MUN$:GOTO 830
791 SOUND 150,.1:SOUND 350,.1
830 IF NV<1 THEN GOTO 1480:ELSE IF NV<50 THEN F=F/2
840 RETURN
850 REM
860 REM   collision between pac-girl and a monster
870 REM
880 IF SCREEN(X,1+Y+Y,1)>7 THEN 1020
890 PLAY "mbl8t255o4fego3abcdefgo0l1g-g"
900 LOCATE P(1,3),1+2*P(1,4):PRINT CHR$(P(1,5));:COLOR 26,0
910 FOR N=1 TO 200: LOCATE X,1+Y+Y:PRINT CHR$(168);:NEXT:IF NV>300 THEN IF F<.1 THEN F=F+F
920 COLOR 7,0:IF NC>1 THEN NC=NC-1:LOCATE 25,15:PRINT STRING$(NC,1);B;B;B;B;,ELSE 1530
930 FOR N=1 TO 5:LOCATE P(N,3),1+2*P(N,4):PRINT CHR$(P(N,5)):P(N,5)=32:P(N,2)=7:P(N,3)=14:P(N,4)=17+N:P(N,6)=0:IF N=1 THEN P(N,7)=0:P(N,8)=0
970 P(N,9)=0:P(N,10)=0:NEXT
980 P(1,3)=19:P(1,4)=19:IN=FNLAP%(2):RETURN
1000 '     the monster is edible
1020 PLAY "mbl24o2xb2;l32o3xb2;l64o4xb2;":LOCATE P(1,3),1+2*P(1,4):PRINT CHR$(P(1,5));:COLOR 26,0:FOR N=1 TO 100:LOCATE X,1+Y+Y:PRINT CHR$(2);:NEXT:IN=FNLAP%(2):F=F/2:COLOR 7,0:IF NC<3 THEN NC=NC+1:LOCATE 25,15:PRINT STRING$(NC,1);B;B;B;B;
1060 FOR N=2 TO 5:P(N,6)=P(N,6)-1
1080 IF P(N,3)=X AND P(N,4)=Y THEN P(N,2)=7:P(N,3)=14:P(N,4)=16+N: IF P(N,5)=249 THEN NV=NV-1: LOCATE 25,5:PRINT NV;:P(N,5)=32: ELSE P(N,5)=32
1090 NEXT:GOTO 790
1120 'move the monsters
1140 FOR N=2 TO 5:P(N,6)=P(N,6)-1:X=P(N,3):Y=P(N,4):IF Y<18 THEN 1170
1142 IF Y>23 THEN 1170
1164 IF X=11 THEN 1240
1165 IF X=12 THEN 1240
1166 IF X<=12 THEN 1170
1167 IF X<16 THEN 1270
1170 IF RND(2)<F THEN 1240
1180 IF P(N,7)<>0 THEN 1210
1181 IF P(1,3)=P(N,3) THEN 1210
1190 X=X+SGN(P(1,3)-X):W=SCREEN(X,1+Y+Y):IF W=32 THEN 1380
1191 IF W=249 THEN 1380
1200 IF W=1 THEN 1440
1210 IF P(N,8)<>0 THEN 1240
1211 IF P(1,4)=P(N,4) THEN 1240
1220 X=P(N,3):Y=Y+SGN(P(1,4)-Y):W=SCREEN(X,1+Y+Y):IF W=32 THEN 1380
1221 IF W=249 THEN 1380
1230 IF W=1 THEN 1440
1240 X=P(N,3)+P(N,7):Y=P(N,4)+P(N,8):W=SCREEN(X,1+Y+Y)
1250 IF W=32 THEN 1380
1251 IF W=249 THEN 1380
1260 IF W=P(1,1) THEN 1440
1270 T=RND(2):IF T=0 THEN T=-1
1280 X=P(N,3)+P(N,8)*T:Y=P(N,4)+P(N,7)*T:W=SCREEN(X,1+Y+Y):IF W=32 THEN 1380
1281 IF W=249 THEN 1380
1300 IF W=P(1,1) THEN 1440
1310 X=P(N,3)-P(N,8)*T:Y=P(N,4)-P(N,7)*T:W=SCREEN(X,1+Y+Y):IF W=32 THEN 1380
1311 IF W=249 THEN 1380
1330 IF W=P(1,1) THEN 1440
1340 X=P(N,3)-P(N,7):Y=P(N,4)-P(N,8):W=SCREEN(X,1+Y+Y):IF W=32 THEN 1380
1341 IF W=249 THEN 1380
1360 IF W=P(1,1) THEN 1440
1370 X=P(N,3):Y=P(N,4):W=P(N,5):GOTO 1390
1380 P(N,7)=X-P(N,3):P(N,8)=Y-P(N,4)
1390 LOCATE P(N,3),1+P(N,4)*2:PRINT CHR$(P(N,5));
1400 IF P(N,6)<=0 THEN P(N,2)=7
1410 COLOR P(N,2),0:LOCATE X,1+Y+Y:PRINT CHR$(P(N,1));:COLOR 7,0:P(N,5)=W:P(N,3)=X:P(N,4)=Y:NEXT:RETURN
1440 X=P(N,3):Y=P(N,4):W=1+N:GOTO 880
1460 '  pac-girl  beat it...
1480 LOCATE 25,10:COLOR 26,0:PRINT "Tebrikler !!!";:COLOR 7,0:IN=FNLAP%(2)
1490 IF FNLAP%(2)-IN<15 THEN GOTO 1490
1500 REM
1510 REM    pac-girl defeated...
1520 REM
1530 LOCATE 25,10:COLOR 26,0:PRINT "Tekrar ...???";:COLOR 7,0:POKE 106,0
1540 AA=INKEY$:IF AA="" THEN GOTO 1540
1550 IF (AA="y") OR (AA="Y") THEN GOTO 12:ELSE IF AA<>"n" THEN GOTO 1560
1560 CLS: PRINT:PRINT:PRINT "OYUN B˜TT˜ ˜Y˜ GšNLER D˜LER":END
